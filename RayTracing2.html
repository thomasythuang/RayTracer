<html>

<head>
<title>Ray tracer using WebGL</title>
<meta http-equiv="content-type" content="text/html; charset=ISO-8859-1">

<!--  I chose to write shaders as html scripts, which removes the need to 
      use the quotes notation used when writing them in the js file-->

<!-- Vertex Shader -->
<script id="shader-vs" type="x-shader/x-vertex">
  attribute vec2 aVertexPosition;
  attribute vec3 aPlotPosition;

  varying vec3 vPosition;

  void main(void)
  {
    gl_Position = vec4(aVertexPosition, 1.0, 1.0);
    vPosition = aPlotPosition;
 }
</script>

<!--  Fragment Shader: Calculating the rays in the shaders 
      removes the need for textures -->
<script id="shader-fs" type="x-shader/x-fragment">
  precision mediump float;

  const vec3 lightDir = vec3(0.577350269, 0.577350269, -0.577350269);
  varying vec3 vPosition;
  uniform vec3 cameraPos;
  uniform vec3 sphere1Center;
  uniform vec3 sphere2Center;
  uniform vec3 sphere3Center;
  uniform vec3 sphere4Center;

  //Calculate the intersection (or non-intersection) or a ray and a sphere
  bool intersectSphere(vec3 center, vec3 lStart, vec3 lDir,
                       out float dist) {
    vec3 c = center - lStart;
    float b = dot(lDir, c);
    float d = b*b - dot(c, c) + 1.0;
    if (d < 0.0) {
      dist = 10000.0;
      return false;
    }

    dist = b - sqrt(d);
    if (dist < 0.0) {
      dist = 10000.0;
      return false;
    }

    return true;
  }

  vec3 lightAt(vec3 N, vec3 V, vec3 color) {
    vec3 L = lightDir;
    vec3 R = reflect(-L, N);

    float c = 0.3 + 0.4 * pow(max(dot(R, V), 0.0), 30.0) + 0.7 * dot(L, N);

    if (c > 1.0) {
      return mix(color, vec3(1.6, 1.6, 1.6), c - 1.0);
    }

    return c * color;
  }

  //Calculate intersection of rays with the whole scene
  bool intersectWorld(vec3 lStart, vec3 lDir, out vec3 pos,
                      out vec3 normal, out vec3 color) {
    float d1, d2, d3, d4;
    bool h1, h2, h3, h4;

    //Calculate if spheres intersect with rays
    h1 = intersectSphere(sphere1Center, lStart, lDir, d1);
    h2 = intersectSphere(sphere2Center, lStart, lDir, d2);
    h3 = intersectSphere(sphere3Center, lStart, lDir, d3);
    h4 = intersectSphere(sphere4Center, lStart, lDir, d4);

    //If hitting a sphere, set color of the front-most sphere
    if (h1 && d1 < d2 && d1 < d3 && d1 < d4) {
      pos = lStart + d1 * lDir;
      normal = pos - sphere1Center;
      color = vec3(1.0, 0.5725, 0.0);
    }
    else if (h2 && d2 < d3 && d2 < d4) {
      pos = lStart + d2 * lDir;
      normal = pos - sphere2Center;
      color = vec3(1.0, 0.749, 0.0);
    }
    else if (h3 && d3 < d4) {
      pos = lStart + d3 * lDir;
      normal = pos - sphere3Center;
      color = vec3(0.106, 0.106, 0.702);
    }
    else if (h4){
      pos = lStart + d4 * lDir;
      normal = pos - sphere4Center;
      color = vec3(0.043, 0.38, 0.517);
    }
    else if (lDir.y < -0.01) {
      pos = lStart + ((lStart.y + 2.7) / -lDir.y) * lDir;
      if (pos.x*pos.x > 80.0) {
        return false;
      }
      else if (pos.z*pos.z > 80.0) {
        return false;
      }
      normal = vec3(0.0, 1.0, 0.0);
      if (fract(pos.x / 2.0) > 0.5 == fract(pos.z / 2.0) > 0.5) {
        color = vec3(0.8);
      }
      else {
        color = vec3(0.4);
      }
    }
    else {
     return false;
    }

    return true;
  }

  void main(void)
  {
    vec3 cameraDir = normalize(vPosition - cameraPos);

    vec3 p1, norm, p2;
    vec3 col, colT, colM, col3;
    if (intersectWorld(cameraPos, cameraDir, p1,
                       norm, colT)) {
      col = lightAt(norm, -cameraDir, colT);
      colM = (colT + vec3(0.7)) / 1.7;
      cameraDir = reflect(cameraDir, norm);
      if (intersectWorld(p1, cameraDir, p2, norm, colT)) {
        col += lightAt(norm, -cameraDir, colT) * colM;
        colM *= (colT + vec3(0.7)) / 1.7;
        cameraDir = reflect(cameraDir, norm);
        if (intersectWorld(p2, cameraDir, p1, norm, colT)) {
          col += lightAt(norm, -cameraDir, colT) * colM;
        }
      }
      gl_FragColor = vec4(col, 1.0);
    }
    else {
      gl_FragColor = vec4(0.0, 0.0, 0.0, 1.0);
    }
  }
</script>
</head>

<body onload="main(); runToggle()" style="height: 100%; margin: 0">  
  <div style="height:100%; margin: 0;">
    <div style="text-align:center; padding: 1em;">
      <input type=button onclick="runToggle()" value=pause>
    </div>
    <div style="z-index:-1; text-align:center; width:100%;">
      <canvas id="canvas" style="border: none;" width="800" height="400"></canvas>
    </div>
  </div>
  <div style="margin: -8em 1em 0;" id="contrib">
  </div>
  <script src="RayTracing2.js"></script>
  <script src="lib/webgl-utils.js"></script>
  <script src="lib/webgl-debug.js"></script>
  <script src="lib/cuon-matrix-quat.js"></script> <! JT modified to add quaternions -->
</body>

</html>